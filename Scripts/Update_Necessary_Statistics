--更新规则：新表数据有变化；变化数据量大于10%表；排除 history 表；近2天未更新过统计信息(定时任务设置每天更新)
 
SET NOCOUNT ON
IF OBJECT_ID('tempdb..#update_stat') is not null
    DROP TABLE #update_stat
CREATE TABLE #update_stat(id INT IDENTITY(1,1),execsql VARCHAR(300))
GO
DECLARE @dbname VARCHAR(128)
DECLARE @sql    VARCHAR(4000)
DECLARE db_cur CURSOR FOR SELECT name FROM sys.databases WHERE state=0 AND database_id>4
OPEN db_cur
FETCH NEXT FROM db_cur INTO @dbname
WHILE(@@FETCH_STATUS = 0)
BEGIN
    SET @sql  ='
USE ['+@dbname+']
;WITH T AS (
    SELECT OBJECT_SCHEMA_NAME(C.[object_id]) AS schemaname
    ,C.name AS tabname,A.name AS statname,a.[rows] AS tabrows,rowmodctr
    ,stats_date(B.[object_id],B.stats_id) AS update_date
    FROM SYS.SYSINDEXES A
    INNER JOIN SYS.STATS B ON A.name = B.name
    INNER JOIN SYS.TABLES C ON B.[object_id] = C.[object_id]
    WHERE C.name NOT LIKE ''%history%''
) INSERT INTO #update_stat(execsql)
SELECT ''Update STATISTICS ['+@dbname+'].[''+schemaname+''].[''+tabname+''] [''+statname+''] WITH FULLSCAN;''
FROM(
    SELECT schemaname,tabname,statname,tabrows FROM T WHERE tabrows = 0 AND rowmodctr > 0
    AND update_date<DATEADD(D,-2,GETDATE())
    UNION
    SELECT schemaname,tabname,statname,tabrows FROM T WHERE tabrows > 0 AND rowmodctr > 0 AND rowmodctr*1.0/tabrows > 0.1
    AND update_date<DATEADD(D,-2,GETDATE())
) T ORDER BY schemaname,tabname,statname;'
    EXEC(@sql)
FETCH NEXT FROM db_cur INTO @dbname
END
CLOSE db_cur
DEALLOCATE db_cur
GO
DECLARE @i INT = 1,@execsql VARCHAR(300)
WHILE EXISTS(SELECT * FROM #update_stat WHERE id=@i)
BEGIN
    SELECT @execsql = execsql FROM #update_stat WHERE id=@i
    --RAISERROR (@execsql, 0, 1) WITH NOWAIT
    EXEC(@execsql)
    SET @i = @i + 1
END
GO
IF OBJECT_ID('tempdb..#update_stat') is not null
    DROP TABLE #update_stat
GO
SET NOCOUNT OFF
